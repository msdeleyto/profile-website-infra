# Root Terraform configuration
# Orchestrates modules for ECS (EC2-backed) deployment

locals {
  ecr_repository_arns = [
    "arn:aws:ecr:us-east-1:${data.aws_caller_identity.current.account_id}:repository/web/profile"
  ]
  ecs_service_images = {
    web = "${data.aws_caller_identity.current.account_id}.dkr.ecr.us-east-1.amazonaws.com/web/profile:34cdc7bf062150e4b25d9330ce5dcde8ea1529e1"
  }
}

module "network" {
  source = "./modules/network"

  project_name       = var.project_name
  vpc_cidr           = var.vpc_cidr
  availability_zones = var.availability_zones
}

module "iam" {
  source = "./modules/iam"

  project_name        = var.project_name
  ecr_repository_arns = local.ecr_repository_arns
}

module "acm" {
  source = "./modules/acm"

  project_name = var.project_name
  domain_name  = var.domain_name
}

module "alb" {
  source = "./modules/alb"

  project_name          = var.project_name
  vpc_id                = module.network.vpc_id
  alb_security_group_id = module.network.alb_security_group_id
  public_subnet_ids     = module.network.public_subnet_ids
  certificate_arn       = module.acm.certificate_arn

  services = {
    for name, route_config in var.alb_routes : name => {
      container_port         = var.ecs_services[name].container_port
      health_check_path      = route_config.health_check_path
      health_check_matcher   = route_config.health_check_matcher
      health_check_interval  = route_config.health_check_interval
      health_check_timeout   = route_config.health_check_timeout
      healthy_threshold      = route_config.healthy_threshold
      unhealthy_threshold    = route_config.unhealthy_threshold
      deregistration_delay   = route_config.deregistration_delay
      listener_rule_priority = route_config.priority
      path_pattern           = route_config.path_pattern
      host_header            = route_config.host_header
    }
  }
  target_type = "instance"
}

module "waf" {
  source = "./modules/waf"

  project_name = var.project_name
  alb_arn      = module.alb.alb_arn
}

module "ecs" {
  source = "./modules/ecs"

  project_name            = var.project_name
  subnet_ids              = module.network.public_subnet_ids
  ecs_security_group_id   = module.network.ecs_security_group_id
  task_execution_role_arn = module.iam.task_execution_role_arn
  task_role_arn           = module.iam.task_role_arn
  aws_region              = var.aws_region

  service_images = local.ecs_service_images
  services       = var.ecs_services

  alb_target_group_arns     = module.alb.target_group_arns
  ecs_instance_type         = var.ecs_instance_type
  ecs_asg_min               = var.ecs_asg_min
  ecs_asg_max               = var.ecs_asg_max
  ecs_desired_capacity      = var.ecs_desired_capacity
  ecs_ami_id                = var.ecs_ami_id
  ecs_instance_profile_name = module.iam.ecs_instance_profile_name
}
